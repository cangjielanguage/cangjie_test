/*
 * Copyright (c) Huawei Technologies Co., Ltd. 2025. All rights reserved.
 * This source file is part of the Cangjie project, licensed under Apache-2.0
 * with Runtime Library Exception.
 *
 * See https://cangjie-lang.cn/pages/LICENSE for license information.
 */
// EXEC: %compiler %import-cangjie-stdx  %cmp_opt %f -o %output %cmp_utest_opt
// RUN-EXEC-PIPE-0: %set_stdx_path %run %run_opt %output %run_utest_opt %run_args 2>&1

/*
 * Test description:
 *      Test class Server
 *          Test the request body is empty
 * Test API:
 *       public func serve(): Unit
 */

package tests.server1_1

import stdx.net.http.*
import stdx.net.tls
import stdx.crypto.kit

import std.collection.ArrayList
import std.net.TcpSocket

@Test
class TestServer11EmptyBody {
    let server = ServerBuilder().addr("127.0.0.1").port(0).build()
    var port = 0u16

    @TestCase
    func clAndTeTest(): Unit {
        startServer()
        sleep(Duration.second)
        port = server.port
        sendRequest()
        sleep(Duration.millisecond * 100)
        server.close()
    }

    func startServer() {
        server
            .distributor
            .register(
                "/emptyBodyTest",
                {
                    httpContext =>
                    let bodyBuf = Array<UInt8>(10, repeat: 0)
                    let allBody = ArrayList<UInt8>()
                    var readLen = httpContext.request.body.read(bodyBuf)
                    while (readLen > 0) {
                        allBody.add(all: bodyBuf[..readLen])
                        readLen = httpContext.request.body.read(bodyBuf)
                    }
                    @Expect(allBody.toArray().isEmpty())
                    println("ok")
                }
            )
        sleep(Duration.millisecond * 100)
        spawn {
            server.serve()
        }
    }

    func sendRequest() {
        let client = TcpSocket("127.0.0.1", port)
        client.connect()

        let request = "GET /emptyBodyTest HTTP/1.1\r\nHost: 127.0.0.1:${port}\r\nContent-Length:\r\nConnection: keep-alive\r\n\r\n"
            .toArray()
        client.write(request)

        sleep(Duration.millisecond * 400)

        let arr = Array<UInt8>(1000, repeat: 0)
        let len = client.read(arr)
        let resp = String.fromUtf8(arr.slice(0, len))
        @Expect(resp.contains("HTTP/1.1 200 OK\r\n"))
        client.close()
    }
}
