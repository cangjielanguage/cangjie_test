/*
 * Copyright (c) Huawei Technologies Co., Ltd. 2025. All rights reserved.
 * This source file is part of the Cangjie project, licensed under Apache-2.0
 * with Runtime Library Exception.
 * 
 * See https://cangjie-lang.cn/pages/LICENSE for license information.
 */

// LEVEL: 2
// EXEC: %compiler %cmp_opt -o %n.%suffix %f %cmp_utest_opt
// RUN-EXEC: %run %run_opt %n.%suffix %run_utest_opt %run_args

import std.collection.concurrent.*
import std.sync.*

var cmap = Option<ConcurrentHashMap<Int64, Int64>>.None
let M = 512

@Test
class Test_concurrenthashmap_18 {
    public func PreData(N: Int64): Unit {
        let cmap1 = ConcurrentHashMap<Int64, Int64>()
        let jobs = Array<Future<Unit>>(N, repeat: unsafe { zeroValue<Future<Unit>>() })
        for (t in 0..N) {
            jobs[t] = spawn {
                for (i in t..M : N) {
                    cmap1.put(i, i + 1)
                }
            }
        }
        for (t in 0..N) {
            jobs[t].get()
        }
        cmap = Option<ConcurrentHashMap<Int64, Int64>>.Some(cmap1)
    }

    @TestCase
    func test_01(): Unit {
        //test the concurrent of func [](key: K, value!: V) with unexist key.
        let threads = [2]
        for (N in threads) {
            PreData(N)
            let jobs = Array<Future<Unit>>(N, repeat: unsafe { zeroValue<Future<Unit>>() })
            for (t in 0..N) {
                jobs[t] = spawn {
                    for (i in t..M : N) {
                        if (let Some(s) <- cmap) {
                            try {
                                s[M + i]
                            } catch (e: NoneValueException) {
                                @Expect(e.message.contains("Value does not exist"))
                            }
                        }
                    }
                }
            }
            for (t in 0..N) {
                jobs[t].get()
            }
        }
    }

    @TestCase
    func test_02(): Unit {
        //test the concurrent of func [](key: K, value!: V) with unexist and exist key.
        let threads = [2, 4, 8, 12, 16, 20, 24, 28, 32, 36]
        for (N in threads) {
            PreData(N)
            let jobs = Array<Future<Unit>>(N, repeat: unsafe { zeroValue<Future<Unit>>() })
            for (t in 0..N) {
                jobs[t] = spawn {
                    for (i in t..2 * M : N) {
                        if (let Some(s) <- cmap) {
                            if (i <= M - 1) {
                                @Expect(s[i], i + 1)
                            } else {
                                try {
                                    s[i]
                                } catch (e: NoneValueException) {
                                    @Expect(e.message.contains("Value does not exist"))
                                }
                            }
                        }
                    }
                }
            }
            for (t in 0..N) {
                jobs[t].get()
            }
        }
    }
}
